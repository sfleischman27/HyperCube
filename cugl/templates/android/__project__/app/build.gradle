// GRADLE BUILD SETTINGS FOR ANDROID PROJECT
//
// Adapted from the default SDL project to support asset directories and more.
//
// Author: Walker M. White
// Date: 11/21/22
apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "__NAMESPACE__"
        minSdk 26
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            // We have been unable to get SDL_image to work with CMake, so we are staying with ndkBuild for now
            ndkBuild {
                arguments  'APP_PLATFORM=android-26'
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                cppFlags '-std=c++17', '-fexceptions', '-frtti', '-Wno-conversion-null',  '-Wno-deprecated', '-Wno-deprecated-declarations'
                cFlags '-Wno-conversion-null',  '-Wno-deprecated', '-Wno-deprecated-declarations'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        abortOnError false
    }
    namespace '__NAMESPACE__'
    sourceSets.main {
        jniLibs.srcDir 'libs'
        assets.srcDirs += "__ASSET_DIR__"
    }
    externalNativeBuild {
        // We have been unable to get SDL_image to work with CMake, so we are staying with ndkBuild for now
        ndkBuild {
            path 'jni/Android.mk'
        }
    }
    // Our package structure for SDL2 has ordering requirements
    packagingOptions {
        pickFirst 'lib/x86/libSDL2.so'
        pickFirst 'lib/x86_64/libSDL2.so'
        pickFirst 'lib/armeabi-v7a/libSDL2.so'
        pickFirst 'lib/arm64-v8a/libSDL2.so'
        pickFirst 'lib/x86/libmain.so'
        pickFirst 'lib/x86_64/libmain.so'
        pickFirst 'lib/armeabi-v7a/libmain.so'
        pickFirst 'lib/arm64-v8a/libmain.so'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
